apply plugin: 'base'

version = '1.0'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
        classpath 'org.akhikhl.gretty:gretty:1.1.1'
    }
}

ext {
    javaVersion = 1.8
    slf4jVersion = '1.7.5'
    springVersion = '4.0.8.RELEASE'
    springjpaVersion = '1.7.0.RELEASE'
    querydslVersion = '3.3.2'
    hibernateVersion = '4.3.6.Final'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'eclipse'
    apply plugin: 'eclipse-wtp'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'war'
    apply plugin: 'org.akhikhl.gretty'

    sourceCompatibility = rootProject.ext.javaVersion
    targetCompatibility = rootProject.ext.javaVersion

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://github.com/spring-projects/spring-framework' }
        maven { url 'http://repo.spring.io/milestone' }
    }

    /* start sourceSets */
    String hostname = InetAddress.getLocalHost().getHostName().toLowerCase();
    if (hostname.endsWith('.local')) {
        hostname = hostname.replace(".local", '')
    }

    sourceSets {
        main.java.srcDirs = ['src/main/java']
        test.java.srcDirs = ['src/test/java']
        main.resources.srcDirs = ['src/main/resources', "src/main/resources-" + hostname]
        test.resources.srcDirs = ['src/test/resources', "src/test/resources-" + hostname]
        generated.java.srcDirs = ['src/main/generated']
    }

    sourceSets.all {
        set ->
            set.allSource.srcDirs.each {
                if (!it.exists()) {
                    it.mkdirs()
                }
            }
    }
    /* end sourceSets */

    /* start logs folder create */
    def logsDir = new File('logs')
    if (!logsDir.isDirectory()) {
        logsDir.mkdirs()
    }
    /* end logs folder create */

    configurations {
        all*.exclude group: 'org.hibernate.javax.persistence', module: 'hibernate-jpa-2.0-api'
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.6'
        compile 'org.slf4j:jcl-over-slf4j:1.7.6'
        compile 'ch.qos.logback:logback-classic:1.0.13'
        compile 'ch.qos.logback:logback-core:1.0.13'

        compile "org.slf4j:jcl-over-slf4j:${rootProject.ext.slf4jVersion}"
        compile "org.slf4j:log4j-over-slf4j:${rootProject.ext.slf4jVersion}"
        compile "org.slf4j:slf4j-api:${rootProject.ext.slf4jVersion}"

        /* vertx */
        compile 'io.vertx:vertx-core:2.1.5'
        compile 'io.vertx:vertx-platform:2.1.5'

        /* spring */
        compile "org.springframework:spring-context:${rootProject.ext.springVersion}"
        compile "org.springframework:spring-context-support:${rootProject.ext.springVersion}"
        compile "org.springframework:spring-aspects:${rootProject.ext.springVersion}"
        compile "org.springframework:spring-jdbc:${rootProject.ext.springVersion}"
        compile "org.springframework:spring-orm:${rootProject.ext.springVersion}"
        compile "org.springframework:spring-webmvc:${rootProject.ext.springVersion}"
        compile "org.springframework:spring-websocket:${rootProject.ext.springVersion}"
        compile "org.springframework:spring-messaging:${rootProject.ext.springVersion}"

        /* lombok is getter and setter */
        provided 'org.projectlombok:lombok:1.12.6'

        /* hibernate and jpa */
        compile "org.hibernate:hibernate-core:${rootProject.ext.hibernateVersion}"
        compile "org.hibernate:hibernate-entitymanager:${rootProject.ext.hibernateVersion}"
        compile "org.hibernate:hibernate-ehcache:${rootProject.ext.hibernateVersion}"
        compile "org.hibernate:hibernate-validator:5.1.2.Final"
        compile 'org.hibernate:hibernate-search-engine:4.5.1.Final'

        /* jdbc driver */
        compile 'com.zaxxer:HikariCP:2.0.1'

        compile 'org.apache.httpcomponents:httpclient:4.2.5'
        compile 'org.apache.commons:commons-lang3:3.3.2'
        compile 'org.javassist:javassist:3.18.2-GA'

        /* querydsl and spring jpa */
        compile("com.mysema.querydsl:querydsl-core:$rootProject.ext.querydslVersion")
        compile("com.mysema.querydsl:querydsl-jpa:$rootProject.ext.querydslVersion")
        compile("com.mysema.querydsl:querydsl-sql:$rootProject.ext.querydslVersion")
        provided("com.mysema.querydsl:querydsl-apt:$rootProject.ext.querydslVersion") {
            exclude group: 'com.google.guava'
        }
        compile "org.springframework.data:spring-data-jpa:$rootProject.ext.springjpaVersion"

        /* servlet */
        providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
        providedCompile 'javax.websocket:javax.websocket-api:1.0'
        providedCompile 'javax.servlet:jsp-api:2.0'

        /* test */
        testCompile "junit:junit:4.11"
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'org.unitils:unitils-core:3.4.2'
        testCompile "org.springframework:spring-test:${rootProject.ext.springVersion}"

    }

    task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
        source = sourceSets.main.java
        classpath = configurations.compile + configurations.provided
        options.compilerArgs = [
                "-proc:only",
                "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
        ]
        destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
    }

    compileJava {
        dependsOn generateQueryDSL
        source sourceSets.generated.java.srcDirs.iterator().next()
    }

    compileGeneratedJava {
        dependsOn generateQueryDSL
        options.warnings = false
        classpath += [ sourceSets.main.runtimeClasspath ]
    }

    clean {
        delete sourceSets.generated.java.srcDirs
    }

    idea {
        module {
            sourceDirs += [ file('src/main/generated') ]
        }
    }

    eclipse {
        jdt {
            sourceCompatibility = rootProject.ext.javaVersion
            targetCompatibility = rootProject.ext.javaVersion
        }
    }
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    /* Gretty running task */
    /* url : https://github.com/akhikhl/gretty */
    /* configuration : http://akhikhl.github.io/gretty-doc/Gretty-configuration.html */
    gretty {
        servletContainer = 'tomcat8'
        contextPath = ''
        managedClassReload = false
        recompileOnSourceChange = false
        reloadOnClassChange = false
        reloadOnConfigChange = false
        reloadOnLibChange = false
        host = 'localhost'
        httpPort = 8000
    }

}

task help << {
    println '--------- gradle tomcat command help ----------------------'
    println 'gradle :tomcatRun : local에서 tomcat8을 이용한 서버 실행'
    println 'gradle :appRun : local에서 tomcat8을 이용한 서버 실행'
}